---
title: "텍스트 문자열에서 숫자 제거"
description: ""
id: "C151"
no: 151
order: 151
tags: []
created: "2025-05-12T04:11:54.584Z"
updated: "2025-05-12T04:28:42.528Z"
featured: false
draft: true
hasChildren: false
elementCount: 66
childCount: 0
slug: "C151"
---

# 텍스트 문자열에서 숫자 제거



텍스트 문자열 목록에서만 모든 숫자를 제거하고 숫자가 아닌 다른 문자는 유지하려는 경우 Excel의 일부 수식이 도움이 될 수 있습니다.

![file](/images/3fe729314e2b89906f44c50d1f823263.jpg)



### ※ Excel의 텍스트 문자열에서 모든 숫자 제거



영숫자 문자열에서 숫자만 제거하려면 TEXTJOIN, MID, ROW, IF, ISERR, LEN 및 INDIRECT 함수를 결합한 복잡한 배열 수식이 도움이 될 수 있습니다. 

**주의 사항**:이 수식은 Excel 2019 및 Office 365에서만 잘 작동합니다. 일반 구문은 다음과 같습니다.

=TEXTJOIN("",TRUE,IF(ISERR(MID(text,ROW(INDIRECT("1:"&LEN(text))),1)+0),MID(text,ROW(INDIRECT("1:"&LEN(text))),1),""))

- **text** : 모든 숫자를 제거 할 텍스트 문자열 또는 셀 값입니다.
**1**. 결과를 출력하려는 ​​빈 셀에 아래 수식을 복사하거나 입력하십시오.

=TEXTJOIN("",TRUE,IF(ISERR(MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1)+0),MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1),""))

**2**. 그런 다음, **Ctrl + Shift + Enter** 키를 동시에 눌러 첫 번째 결과를 얻으려면 스크린 샷을 참조하십시오.

![file](/images/c43a849a21f3cbb36d89af19cca5c19c.jpg)

**3**. 그런 다음 수식 셀을 선택하고 채우기 핸들을 사용하려는 셀로 드래그하면 텍스트 문자열에서 모든 숫자가 제거되었습니다. 스크린 샷을 참조하십시오.

![file](/images/b7aa94c5e131390e580efd1f06248432.jpg)



**공식 설명 :**

**행 (간접 ( "1 :"& LEN (A2))) **: ROW 함수 내의 INDIRECT 함수는 아래와 같이 A2 셀의 길이 인 배열 목록을 반환하며,이 부분은 MID 함수에서 start_num으로 인식됩니다. {1;2;3;4;5;6;7;8;9;10;11;12;13;14;15;16;17;18;19;20;21}

**MID (A2, ROW (INDIRECT ( "1 :"& LEN (A2))), 1) **: 이 MID 함수는 A2 셀에서 텍스트를 추출하여 한 문자를 가져 오는 데 사용되며 다음과 같은 배열을 반환합니다. 

{ "5"; "0"; "0"; ""; "K"; "u"; "t"; "o"; "o"; "l"; "s"; ""; "f" ; "o"; "r"; ""; "E"; "x"; "c"; "e"; "l"}

**MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1)+0 **: 이 배열을 사용하여 텍스트를 숫자로 강제 변환 한 후 0 값을 추가하면 숫자 텍스트 값이 숫자로 변환되고 숫자가 아닌 값은     다음과 같이 #VALUE 오류 값으로 표시됩니다. 

{ "5"; "0"; "0"; # VALUE!; # VALUE!; # VALUE!; # VALUE!; # VALUE!; # VALUE! !;#값! !;#값!...}

**IF(ISERR(MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1)+0),MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1),"") **: IF 함수는 MID 함수에서 반환 된 배열이 오류 값인지 여부를 확인하는 데 사용됩니다. 오류 값이면 두 번째 MID 함수를 사용하여 숫자가 아닌 문자를 가져옵니다. 그렇지 않으면 다음과 같이 빈 문자열이 표시됩니다. 

{ ""; ""; ""; ""; "K"; "u"; "t"; "o"; "o"; "l"; "s"; ""; "f"; "o ";"r ";" ";"E ";"x ";"c ";"e ";"l "}

**TEXTJOIN("",TRUE,IF(ISERR(MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1)+0),MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1),"")) **: 마지막으로 TEXTJOIN 함수는 

모든 값을 결합하고 결과를 얻기 위해 빈 문자열을 무시합니다.



**배송 시 요청 사항 :**

1. 숫자를 제거 할 때 여분의 공백 문자가 남아있을 수 있습니다. 여분의 공백을 모두 제거하려면 수식을 아래와 같이 TRIM 함수로 래핑해야합니다.

=TRIM(TEXTJOIN("",TRUE,IF(ISERR(MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1)+0),MID(A2,ROW(INDIRECT("1:"&LEN(A2))),1),"")))

2. Excel 초기 버전의 텍스트 문자열에서 숫자를 제거하려면 다음 공식을 적용하십시오.

=SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(SUBSTITUTE(A2,1,""),2,""),3,""),4,""),5,""),6,""),7,""),8,""),9,""),0,"")

![file](/images/8aa0956f05fe80eb39cf9c42cd46b5f2.jpg)
